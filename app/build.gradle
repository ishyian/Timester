plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdk globalConfiguration["androidCompileSdkVersion"]

    defaultConfig {
        applicationId "com.melitopolcherry.timester"
        minSdk globalConfiguration["androidMinSdkVersion"]
        targetSdk globalConfiguration["androidTargetSdkVersion"]
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "topii_${variant.name}-${variant.versionName}-(${variant.versionCode}).apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def notations = rootProject.ext.coreDependencies

    for (item in notations.forImplementation) {
        implementation item
    }

    for (item in notations.forKapt) {
        kapt item
    }

    for (item in notations.forTest) {
        testImplementation item
    }

    for (item in notations.forAndroidTest) {
        androidTestImplementation item
    }

    implementation 'com.github.prolificinteractive:material-calendarview:2.0.1'
    implementation 'com.github.SimpleMobileTools:Simple-Commons:58574dd952'
    implementation 'io.github.farhad:contactpicker:2.0.0'
}